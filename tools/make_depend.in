#! /usr/bin/perl -w

# Dieter Deyke
# November 28, 1997
# Copyright (c) 1997-2000 CoCreate Software GmbH. All Rights Reserved.
# Company Confidential

# @(#) $Id: make_depend.in,v 1.1 2000-03-04 18:31:15 deyke Exp $

require 5.003;

use Symbol;

use strict;

require 'tools/perlsubs/lsdir';
require 'tools/perlsubs/selectfile';

use vars '%files';
use vars '%i';

#------------------------------------------------------------------------------

# Scan one file for include files

sub scanfile {

	my ($filename) = @_;

	my ($dir);
	my ($fh);
	my ($file);
	my ($found);

	return if exists($files{$filename});
	$files{$filename} = [$filename];
	$fh = gensym();
	open($fh, $filename) or die("$filename: $!\n");
	while ($file = <$fh>) {
		if ($file =~ s/^\#\s*include\s*\"([^\"]+)\".*\n/$1/) {
			$found = 0;
			foreach $dir ('.', '../lib') {
				if (-f "$dir/$file") {
					$found = 1;
					$file = "$dir/$file" if $dir ne '.';
					push(@{$files{$filename}}, $file);
					scanfile($file);
					last;
				}
			}
			print("While scanning $filename: $file not found\n") if !$found;
		}
	}
	close($fh);
}

#------------------------------------------------------------------------------

sub roll_up {

	my ($filename) = @_;

	my ($file);

	foreach $file (@{$files{$filename}}) {
		if (!exists($i{$file})) {
			$i{$file} = 1;
			roll_up($file);
		}
	}
}

#------------------------------------------------------------------------------

sub make_depend {

	my ($fhi);
	my ($fho);
	my ($file);
	my ($makefile) = 'Makefile';
	my ($obj);
	my ($tmpmakefile) = 'Makefile.tmp';

	%files = ();
	foreach $file (grep(/\.c$/, lsdir('.'))) {
		scanfile($file);
	}

	$fhi = gensym();
	open($fhi, $makefile) or die("$makefile: $!\n");
	$fho = gensym();
	open($fho, ">$tmpmakefile") or die("$tmpmakefile: $!\n");
	while (<$fhi>) {
		last if $_ eq "###\n";
		print($fho $_);
	}
	close($fhi);
	print($fho "###\n");
	foreach $file (sort(grep(/\.c$/, keys(%files)))) {
		($obj = $file) =~ s!\.c$!.o!;
		print($fho "$obj:");
		%i = ();
		roll_up($file);
		foreach $file (sort(keys(%i))) {
			print($fho "\\\n\t$file");
		}
		print($fho "\n");
	}
	close($fho);
	selectfile($makefile, $tmpmakefile);
}

#------------------------------------------------------------------------------

make_depend();
